
varnishtest "Header-vmod: Test fetching"

server s1 {
	timeout 10

	rxreq
	expect req.url == "/"
	txresp -status 200 -hdr "foo: sillycookie=blah" -hdr "foo: realcookie=YAI"
	rxreq
	expect req.url == "/two"
	txresp -status 200 -hdr "foo: sillycookie=blah" -hdr "foo: realcookie=YAI"
	
	rxreq
	expect req.url == "/three"
	txresp -status 200
} -start

varnish v1 -vcl+backend {
	import header from "${vmod_topbuild}/src/.libs/libvmod_header.so";

	sub vcl_fetch {
		if (req.url == "/") {
			set beresp.http.xusr = header.get(beresp.http.foo,"realcookie=");
		} elsif (req.url == "/two") {
			set beresp.http.xusr = header.get(beresp.http.foo,"^realcookie=");
		} elsif (req.url == "/three") {
			set beresp.http.xusr = header.get(req.http.Cookie, "foo-bar=");
		}
		return(deliver);
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.status == 200
	expect resp.http.xusr == "realcookie=YAI"

	txreq -url "/two"
	rxresp
	expect resp.status == 200
	expect resp.http.xusr == "realcookie=YAI"

	txreq -url "/three" -hdr "Cookie: foo-bar=derp"
	rxresp
	expect resp.status == 200
	expect resp.http.xusr == "foo-bar=derp"
} -run


